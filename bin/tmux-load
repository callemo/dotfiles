#!/bin/sh
# tmux-load: loads a tmux session

f="${1:-$HOME/tmux.dump}"
d='          '

awk '
BEGIN   { FS = "'"$d"'"; prev = -1 }
NR == 1 { spath = $0 }
NR == 2 { sname = $0
          cmd = sprintf("tmux new -d -s '\''%s'\'' -c '\''%s'\''", sname, spath)  }

$1 == "p"  {
    win = $2

    if (win != 0) {
        if (win == prev )
            cmd = cmd sprintf(" \\; splitw -d -c '\''%s'\''", $4)
        else
            cmd = cmd sprintf(" \\; neww -d -c '\''%s'\''", $4)
    }

    prev = win
}

$1 == "w" {
    win = $2

    if ($3 ~ /\*/)
        active = win

    if ($5 == "off")
        cmd = cmd sprintf(" \\; renamew -t %d '\''%s'\''", win, $4)

    # cmd = cmd sprintf(" \\; selectl -t %d '\''%s'\''", win, $6)
}

END     { cmd = cmd " \\; selectw -t " active " \\; attach"
          system(cmd) }' "$f"
