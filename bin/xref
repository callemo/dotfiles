#!/bin/sh
# xref: updates the cross references
grep -ERI '\[\[[A-Za-z0-9_/.-]+\]\]' . |
awk '
BEGIN {
	FS = ":"
	OFS = "\t"
	files()
}

function files(    f,k) {
	while (("find . -type f" | getline f) > 0) {
		k = f
		sub(/^\.\/(R\/)?/, "", k)
		sub(/[^A-Za-z0-9\/].*$/, "", k)
		file[k] = f
	}
	close("find . -type f")
}

$2 ~ /^References/ { next }

{
	s = $1
	sub(/^\.\/(R\/)?/, "", s)
	sub(/[^A-Za-z0-9\/].*$/, "", s)

	l = $0
	while(match(l, /\[\[[A-Za-z0-9_\/]+\]\]/)) {
		t = substr(l, RSTART+2, RLENGTH-4)
		xref[t] = xref[t] " [[" s "]]"
		l = substr(l, RSTART+RLENGTH)
	}
}

END {
	for (i in xref)
		print file[i], xref[i]
}'
